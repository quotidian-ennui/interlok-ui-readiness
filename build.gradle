import org.apache.tools.ant.taskdefs.condition.Os

plugins {
  id "java-library"
  id 'jacoco'
  id 'org.barfuin.gradle.jacocolog' version '3.1.0'
}

ext {
  slf4jVersion = '2.0.7'
  junitJupiterVersion = '5.9.3'
  javaxActivationVersion = '1.2.2'
  interlokConfigFile=project.findProperty("interlokConfigFile") ?: "$projectDir/src/test/resources/adapter.xml"
  uiXpathWorkingDir=project.findProperty("uiXpathWorkingDir") ?: "$projectDir"
  testResourcesDir = { ->
    return "${project.projectDir}/src/test/resources".replaceAll("\\\\", "/")
  }
}

defaultTasks 'clean', 'doWork'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11
group   = 'io.github.quotidian-ennui'

repositories {
  mavenCentral()
}


def asFileUrl(filepath) {
  return "file:///" + new java.net.URI(null, filepath, null).toASCIIString();
}

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 60, "minutes"
  resolutionStrategy.dependencySubstitution {
    substitute module('commons-logging:commons-logging') using module("org.slf4j:jcl-over-slf4j:$slf4jVersion")
    substitute module("net.sf.saxon:saxon") using module ("net.sf.saxon:Saxon-HE:12.1")
    substitute module('jakarta.activation:jakarta.activation-api:1.2.2') using module("com.sun.activation:jakarta.activation:$javaxActivationVersion")
    substitute module('javax.activation:activation') using module("com.sun.activation:jakarta.activation:$javaxActivationVersion")
    substitute module('javax.activation:javax.activation-api') using module("com.sun.activation:jakarta.activation:$javaxActivationVersion")
  }
}

dependencies {
  implementation ("net.sf.practicalxml:practicalxml:1.1.19")
  implementation ("net.sf.saxon:Saxon-HE:12.1")

  implementation ("org.apache.commons:commons-lang3:3.12.0")
  implementation (platform("com.fasterxml.jackson:jackson-bom:2.15.0"))

  implementation ("com.fasterxml.jackson.core:jackson-annotations")
  implementation ("com.fasterxml.jackson.core:jackson-databind")
  implementation ("com.fasterxml.jackson.module:jackson-module-jaxb-annotations")

  implementation ("org.slf4j:slf4j-api:$slf4jVersion")
  runtimeOnly ("org.slf4j:slf4j-simple:$slf4jVersion")
  testImplementation ("org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion")
  testImplementation ("org.junit.jupiter:junit-jupiter:$junitJupiterVersion")
  // Extension pack for @SetEnvironmentVariable
  testImplementation ("org.junit-pioneer:junit-pioneer:2.0.0")
  testRuntimeOnly("org.junit.vintage:junit-vintage-engine:$junitJupiterVersion") {
    because 'allows JUnit 3 and JUnit 4 tests to run'
  }
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion")
  testRuntimeOnly("org.junit.platform:junit-platform-launcher:1.9.3") {
    because 'allows tests to run from IDEs that bundle older version of launcher'
  }

}

task LauncherJar(type: Jar) {
    archiveAppendix = "launcher"
    ext.launcherClasspath = { ->
      def verifyLibs = [
              configurations.runtimeClasspath.collect { asFileUrl(it.getCanonicalPath()) }.join(' '),
              asFileUrl(jar.archivePath.getCanonicalPath())
              ]
      return verifyLibs.join(' ')
    }
    manifest {
        attributes ("Class-Path": launcherClasspath())
    }
}

task doWork(type: JavaExec, dependsOn: [jar, LauncherJar]) {
    group = 'Execution'
    description = 'Emit XPaths for config-project.json'
    classpath = files(LauncherJar.archivePath)
    mainClass = 'io.github.quotidianennui.EmitVariableXpaths'
    workingDir= uiXpathWorkingDir
    args interlokConfigFile
}

task deleteGeneratedFiles(type: Delete) {
  delete file(testResourcesDir() + "/adapter.xml.monolithic")
}

test {
  useJUnitPlatform()
}

jacocoTestReport {
  reports {
    xml.required= true
    html.required= true
  }
}

check.dependsOn jacocoTestReport
clean.dependsOn deleteGeneratedFiles
